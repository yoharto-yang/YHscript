COMMENT
##################################################################################
SCRIPT NAME           : LPA00_MAIN_GENERATING_VIEW
CREATED BY            : YOHARTO YANG
CREATED DATE          : 3 JANUARY 2014
PARENT SCRIPT         : -
CHILD SCRIPT          : LPA00_SUB1_GENERATING_VIEW
##################################################################################


COMMENT
################################################
##
## GENERATE VIEW FOR ALL REPORT
##
################################################

IMPORT EXCEL TO LIST_VIEW "LIST_VIEW.fil" FROM "LPA12_REPORT.xlsx" TABLE "LIST_VIEW$" KEEPTITLE FIELD "TABLENAME" C WID 32 AS "" FIELD "VIEWNAME" C WID 32 AS "" FIELD "LISTFIELD" C WID 32 AS ""

OPEN LIST_VIEW
COUNT
ASSIGN VNUM_TOTVIEWLIST = COUNT1
ASSIGN VNUM_COUNTERVIEWLIST = 1

DO SCRIPT LPA00_SUB1_GENERATING_VIEW WHILE VNUM_TOTVIEWLIST >= VNUM_COUNTERVIEWLIST


COMMENT
##################################################################################
SCRIPT NAME           : LPA00_SUB1_GENERATING_VIEW
CREATED BY            : YOHARTO YANG
CREATED DATE          : 3 JANUARY 2014
PARENT SCRIPT         : LPA00_MAIN_GENERATING_VIEW
CHILD SCRIPT          : LPA00_SUB2_GENERATING_VIEW
##################################################################################


OPEN LIST_VIEW
LOCATE RECORD VNUM_COUNTERVIEWLIST

ASSIGN VCHR_VIEWNAME = ALLT(VIEWNAME)
ASSIGN VCHR_TABLENAME = ALLT(TABLENAME)
ASSIGN VCHR_SHEETNAME = ALLT(LISTFIELD)

IMPORT EXCEL TO TESTVIEW "TESTVIEW.fil" FROM "LPA12_REPORT.xlsx" TABLE "%VCHR_SHEETNAME%" KEEPTITLE FIELD "FIELDNAME" C WID 16 AS "" FIELD "ALIAS" C WID 50 AS "" FIELD "POSITION" N WID 2 DEC 0 AS "" FIELD "PIC2" C WID 10 AS "" FIELD "LONG" N WID 10 AS "" 

OPEN TESTVIEW
COUNT
ASSIGN VNUM_TOTREC = COUNT1
ASSIGN VNUM_COUNTER = 1
OPEN %VCHR_TABLENAME% 
DEFINE VIEW %VCHR_VIEWNAME% OK

DO LPA00_SUB2_GENERATING_VIEW WHILE VNUM_TOTREC >= VNUM_COUNTER

VNUM_COUNTERVIEWLIST = VNUM_COUNTERVIEWLIST + 1


COMMENT
##################################################################################
SCRIPT NAME           : LPA00_SUB2_GENERATING_VIEW
CREATED BY            : YOHARTO YANG
CREATED DATE          : 3 JANUARY 2014
PARENT SCRIPT         : LPA00_SUB1_GENERATING_VIEW
CHILD SCRIPT          : -
##################################################################################

OPEN TESTVIEW
LOCATE RECORD VNUM_COUNTER 
ASSIGN VCHR_FIELDNAME = ALLT(FIELDNAME)
ASSIGN VCHR_ALIAS = ALLT(ALIAS)
ASSIGN VNUM_POSITION = POSITION
ASSIGN VCHR_PIC = ALLT(PIC2)
ASSIGN VCHR_WIDTH = LONG

OPEN %VCHR_TABLENAME%
DEFINE COLUMN %VCHR_VIEWNAME% %VCHR_FIELDNAME% AS "%VCHR_ALIAS%" POSITION %VNUM_POSITION% WIDTH %VCHR_WIDTH% PIC "%VCHR_PIC%" 

VNUM_COUNTER = VNUM_COUNTER +1
